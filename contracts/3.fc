{-
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref.
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Flag and the value may be of different lengths.
  When there are overlapping flags, only the first one
  from the overlap needs to be replaced (for example, if the flag is 101,
  value is 111, and string is 10101, then the result would be 11101, but if
  the string is 1010101, then the result would be 1110111).
  Every cell in the input linked list, except for the last one
  (the one that is the deepest in the tree), is guaranteed to be full
  (guaranteed to contain 1023 bits).
  The requirements on the output cell are more loose - only the concatenation of bits
  from the linked list needs to match that of the expected answer, but the structure
  itself may be different (for example, for the purposes of comparing the answer,
  a cell with bit 1 and a ref containing bits 10 is the same as a cell containing
  bit 11 and a ref with bit 0 - they both make up a bitstring 110).
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

(int) tlen (tuple t) asm "TLEN";
(int) ubitsize (int a) asm "UBITSIZE";
int are_slices_equal? (slice a, slice b) asm "SDEQ";

() recv_internal() {
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  int flag_len = ubitsize(flag);
  cell flag_c = begin_cell().store_uint(flag, flag_len).end_cell();
  slice flag_s = flag_c.begin_parse();

  int value_len = ubitsize(value);
  cell value_c = begin_cell().store_uint(value, value_len).end_cell();
  slice value_s = value_c.begin_parse();

  slice current_slice = linked_list.begin_parse();
  builder current_builder = begin_cell();

  tuple result = empty_tuple();

  while(true) {
    if ((current_slice.slice_refs() == 0) & (current_slice.slice_bits() < flag_len)) {
      (current_builder, result) = store_slice_con(current_builder, current_slice, result);
      result~tpush(current_builder.end_cell());
      return tuple_to_cell(result);
    }

    slice next_bit = current_slice.preload_bits(1);
    (current_slice, int is_eq) = is_seq_eq?(current_slice, flag_s);

    ;; WRITE
    if (is_eq) {
      (current_builder, result) = store_slice_con(current_builder, value_s, result);
    } else {
      (current_builder, result) = store_slice_con(current_builder, next_bit, result);
    }
  }

  return  tuple_to_cell(result);
}

(builder, tuple) store_slice_con(builder b, slice s, tuple result) {
  if (s.slice_bits() == 0) {
    return (b, result);
  }

  int builder_bits_left = 1023 - b.builder_bits();
  if (builder_bits_left >= s.slice_bits()) {
    return (b.store_slice(s), result);
  }

  slice first_part = s~load_bits(builder_bits_left);
  result~tpush(b.store_slice(first_part).end_cell());

  return (begin_cell().store_slice(s), result);
}

(slice, int) is_seq_eq?(slice current_slice, slice flag_s) {
  int flag_len = flag_s.slice_bits();
  int is_eq = false;
  int slice_bits_left = current_slice.slice_bits();

  if (slice_bits_left >= flag_len) {
    slice read_slice1 = current_slice.preload_bits(flag_len);
    is_eq = are_slices_equal?(read_slice1, flag_s);
    current_slice~skip_bits(is_eq ? flag_len : 1);

    if ((current_slice.slice_bits() == 0) & (current_slice.slice_refs() != 0)) {
      return (current_slice.preload_ref().begin_parse(), is_eq);
    }
    return (current_slice, is_eq);
  }

  slice read_slice1 = current_slice.preload_bits(slice_bits_left);
  is_eq = are_slices_equal?(read_slice1, flag_s~load_bits(slice_bits_left));

  int bits_to_check_next = flag_len - slice_bits_left;
  slice next_slice = current_slice.preload_ref().begin_parse();

  if (next_slice.slice_bits() < bits_to_check_next) {
    if (slice_bits_left > 1) {
      current_slice~skip_bits(1);
    } else {
      current_slice = next_slice;
    }

    return (current_slice, false);
  }

  slice read_slice2 = next_slice.preload_bits(bits_to_check_next);
  is_eq = is_eq & are_slices_equal?(read_slice2, flag_s);

  if (is_eq) {
    next_slice~skip_bits(bits_to_check_next);
    return (next_slice, true);
  }

  if (slice_bits_left > 1) {
    current_slice~skip_bits(1);
  } else {
    current_slice = next_slice;
  }

  return (current_slice, is_eq);
}

(cell) tuple_to_cell(tuple t) inline {
  int len = t.tlen();
  int i = len - 1;

  cell tail = begin_cell().end_cell();

  while (i >= 0) {
    cell x = t.at(i);
    builder b = begin_cell().store_slice(x.begin_parse());
    if (i != len - 1) {
      tail = b.store_ref(tail).end_cell();
    } else {
      tail = b.end_cell();
    }

    i = i - 1;
  }

  return tail;
}
